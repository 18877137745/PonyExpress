apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "me.wcy.express"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 210
        versionName "2.1.0"
    }

    signingConfigs {
        release {
            storeFile file("wangchenyan.keystore")
            storePassword getLocalValue("STORE_PASSWORD")
            keyAlias getLocalValue("KEY_ALIAS")
            keyPassword getLocalValue("KEY_PASSWORD")
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

def getLocalValue(String key) {
    return getLocalValue(key, false)
}

def getLocalValue(String key, boolean quot) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def value = properties.containsKey(key) ? properties.get(key) : ""
    if (quot) {
        value = "\"" + value + "\"";
    }
    return value
}

ext {
    supportVersion = "25.3.1"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.google.zxing:core:3.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}
